%import .ebl_atf_common (_WORD_SEPERATOR)
?start : dollar_line

?dollar_line : "$" _WORD_SEPERATOR value

?value : state_line | _looses | ruling | image | seal

state_line: [QUALIFICATION _WORD_SEPERATOR] _extent _WORD_SEPERATOR _scopes [_WORD_SEPERATOR STATE] [_WORD_SEPERATOR STATUS]

QUALIFICATION : "at least" | "at most" | "about"

_extent : NUMBER | range | EXTENT
EXTENT : "several" | "some" |  "rest of" | "start of" | "beginning of" | "middle of" | "end of"
range.2 : NUMBER _WORD_SEPERATOR? "-" _WORD_SEPERATOR? NUMBER
NUMBER : DIGIT

_scopes : SCOPE | _object | _surface
SCOPE :  "column" | "columns" | "lines" | "line" | "lines" | "case" | "cases" | "surface" | "side" | "excerpt"

_object : OBJECT | generic_object | fragment
%import .ebl_atf_common (OBJECT)
fragment: "fragment" _WORD_SEPERATOR any_str
generic_object : OBJECT_GENERIC _WORD_SEPERATOR any_str
OBJECT_GENERIC : "object"
any_str.-2 : /./+

_surface : SURFACE | generic_surface | face | edge
%import .ebl_atf_common (SURFACE)
face: "face" _WORD_SEPERATOR LOWER_CASE_LETTER
edge: "edge" _WORD_SEPERATOR LOWER_CASE_LETTER
generic_surface : SURFACE_GENERIC _WORD_SEPERATOR any_str
SURFACE_GENERIC : "surface"

STATE : "blank" | "broken" | "effaced" | "illegible" | "missing" | "traces"
    | "omitted" | "continues"

STATUS : "*" | "?" | "!" | "!?"

_looses: "(" (state_line | ruling | loose) _WORD_SEPERATOR? ")"
loose.-2 : any_str

ruling : RULING_NUMBER _WORD_SEPERATOR RULING
RULING_NUMBER : "single" | "double" | "triple"
RULING : "ruling"

image : _IMAGE _WORD_SEPERATOR DIGIT [LOWER_CASE_LETTER] _WORD_SEPERATOR "=" _WORD_SEPERATOR any_str _WORD_SEPERATOR? ")"
_IMAGE : "(" [_WORD_SEPERATOR] "image"
%import .ebl_atf_common (LOWER_CASE_LETTER)
%import .ebl_atf_common (DIGIT)

%import .ebl_atf_common (seal)
