%import common.ESCAPED_STRING
%import common.SIGNED_NUMBER
%import common.LCASE_LETTER


_WORD_SEPARATOR: " "

?start : dollar_line

?dollar_line : "$" _WORD_SEPARATOR value

?value : strict | _looses | ruling | image

strict : [QUALIFICATION _WORD_SEPARATOR] _extent _WORD_SEPARATOR _scopes [_WORD_SEPARATOR STATE] [_WORD_SEPARATOR STATUS]

QUALIFICATION : "at least" | "at most" | "about"

_extent : NUMBER | range | EXTENT
EXTENT : "several" | "some" |  "rest of" | "start of" | "beginning of" | "middle of" | "end of"
range.2 : NUMBER _WORD_SEPARATOR? "-" _WORD_SEPARATOR? NUMBER
NUMBER : SIGNED_NUMBER

_scopes : SCOPE | _object | _surface
SCOPE :  "column" | "columns" | "lines" | "line" | "lines" | "case" | "cases" | "surface" | "side" | "excerpt"

_object : OBJECT | generic_object | fragment
OBJECT: "tablet" | "envelope" | "prism" | "bulla"
fragment: "fragment" _WORD_SEPARATOR TEXT
generic_object : OBJECT_GENERIC _WORD_SEPARATOR TEXT
OBJECT_GENERIC : "object"
TEXT : /([^\s)]+)/

//have to copy all strict rules and just change TEXT to /[^)]+/ so in a loose line the TEXT of a strict rule doesn't match the closing bracket
// currently no closing brackets in strict (generally) isn't allowed in e.g. TEXT to solve this issue

_surface : SURFACE | generic_surface | face | edge
SURFACE : "obverse" | "reverse" | "left" | "right" | "top" | "bottom" | "face" | "edge"
face: "face" _WORD_SEPARATOR LOWER_CASE_LETTER
edge: "edge" _WORD_SEPARATOR LOWER_CASE_LETTER
generic_surface : SURFACE_GENERIC _WORD_SEPARATOR TEXT
SURFACE_GENERIC : "surface"


STATE : "blank" | "broken" | "effaced" | "illegible" | "missing" | "traces "
    | "omitted" | "continues"

STATUS : "*" | "?" | "!" | "!?"

_looses: "(" (strict | loose) ")"
!loose.-2 : /[^)]+/



ruling : RULING_NUMBER _WORD_SEPARATOR RULING
RULING_NUMBER : "single" | "double" | "triple"
RULING : "ruling"

image : _IMAGE _WORD_SEPARATOR DIGIT [LOWER_CASE_LETTER] _WORD_SEPARATOR "=" _WORD_SEPARATOR TEXTS
_IMAGE : "(" [_WORD_SEPARATOR] "image"
DIGIT : /[0-9]+/
LOWER_CASE_LETTER : /[a-z]/
TEXTS : /./+ ")"

