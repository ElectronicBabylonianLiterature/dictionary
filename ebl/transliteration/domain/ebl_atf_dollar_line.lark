%import common.DIGIT
%import common.INT
%import common.LCASE_LETTER

free_text.-2 : /./+ " "*

?dollar_line: "$" " "* value
?value: strict
      | "(" " "* strict " "* ")"
      | loose
?strict: state | ruling | image | seal
loose: "(" free_text ")"

seal : "seal " INT


state: QUALIFICATION [" " extent] [" " scope] [" " STATE] [" " DOLLAR_STATUS]
     | extent [" " scope] [" " STATE] [" " DOLLAR_STATUS] -> state_extent
     | scope [" " STATE] [" " DOLLAR_STATUS] -> state_scope
     | STATE [" " DOLLAR_STATUS] -> state_state
     | DOLLAR_STATUS -> state_status

QUALIFICATION : "at least" | "at most" | "about"

?extent: INT | range | EXTENT
EXTENT: "several" | "some" |  "rest of" | "start of" | "beginning of" | "middle of" | "end of"
range.2: INT " "? "-" " "? INT

?scope: SCOPE | object | surface
SCOPE:  "column" | "columns" | "lines" | "line" | "lines" | "case" | "cases" | "surface" | "side" | "excerpt"

?object: OBJECT | generic_object | fragment
OBJECT: "tablet" | "envelope" | "prism" | "bulla"
fragment: "fragment " free_text
generic_object : "object " free_text

?surface: SURFACE | generic_surface | face | edge
SURFACE: "obverse" | "reverse" | "left" | "right" | "top" | "bottom" | "face" | "edge"
face: "face " LCASE_LETTER
edge: "edge " LCASE_LETTER
generic_surface : "surface " free_text

STATE: "blank" | "broken" | "effaced" | "illegible" | "missing" | "traces"
     | "omitted" | "continues"

DOLLAR_STATUS: "!?" | "*" | "?" | "!"


ruling : RULING_NUMBER " ruling" [" " DOLLAR_STATUS]
RULING_NUMBER : "single" | "double" | "triple"


image.1 : "(" " "? "image " DIGIT [LCASE_LETTER] " = " free_text ")"
