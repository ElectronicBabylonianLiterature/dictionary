?start: line

?line: empty_line
     | control_line
     | text_line

empty_line: /\s+/?

!control_line: ("=:" | "$" | "@" | "&" | "#") /.+/?

text_line: LINE_NUMBER _WORD_SEPARATOR+ text _WORD_SEPARATOR*
LINE_NUMBER: /[^ ]+/ "."


text: _head _tail* line_continuation?
_head: _have_both
     | _require_both
     | _omit_left
     | _omit_right+ _have_both?
_tail: _WORD_SEPARATOR _omit_right* _have_both?
     | _WORD_SEPARATOR? _omit_left+
     | _WORD_SEPARATOR _require_both

line_continuation: _WORD_SEPARATOR* LINE_CONTINUATION
LINE_CONTINUATION: "→"

_require_both: commentary_protocol
             | divider
_have_both: tabulation
         | COLUMN
         | language_shift
         | erasure
         | word
         | lone_determinative
         | unknown_number_of_signs
_omit_left.-1: cba
          | cpba
          | co
          | cdog
_omit_right.-1: oba
           | opba
           | oo
           | odog

divider.5: DIVIDER_SYMBOL modifiers flags
cba: CLOSE_BROKEN_AWAY
cpba: CLOSE_PERHAPS_BROKEN_AWAY
co: CLOSE_OMISSION
cdog: CLOSE_DOCUMENT_ORIENTED_GLOSS
oba: OPEN_BROKEN_AWAY
opba: OPEN_PERHAPS_BROKEN_AWAY
oo: OPEN_OMISSION
odog: OPEN_DOCUMENT_ORIENTED_GLOSS
language_shift: SHIFT
unknown_number_of_signs: UNKNOWN_NUMBER_OF_SIGNS
omission_or_removal.-1: OMISSION
broken_away.-1: BROKEN_AWAY
perhaps_broken_away.-2: PERHAPS_BROKEN_AWAY

flags: (UNCERTAIN | CORRECTION | COLLATION| DAMAGE)*
modifiers: MODIFIER*

_NEW_LINE: /\n/

tabulation: TABULATION
TABULATION: "($___$)"

COLUMN: "&" ("0".."9")*

commentary_protocol: PROTOCOL_QUOTATION
                   | PROTOCOL_BASE_TEXT
                   | PROTOCOL_COMMENTARY
                   | PROTOCOL_UNCERTAIN
PROTOCOL_QUOTATION: "!qt"
PROTOCOL_BASE_TEXT: "!bs"
PROTOCOL_COMMENTARY: "!cm"
PROTOCOL_UNCERTAIN: "!zz"

DOCUMENT_ORIENTED_GLOSS: OPEN_DOCUMENT_ORIENTED_GLOSS
                       | CLOSE_DOCUMENT_ORIENTED_GLOSS
OPEN_DOCUMENT_ORIENTED_GLOSS: "{("
CLOSE_DOCUMENT_ORIENTED_GLOSS: ")}"

SHIFT: "%" ("a".."z" | "A".."Z" | "0".."9")+

erasure.-1: "°" erasure_part "\\" erasure_part "°"
erasure_part: ((DIVIDER | word | lone_determinative) (_WORD_SEPARATOR (DIVIDER | word | lone_determinative))*)?

PERHAPS_BROKEN_AWAY: OPEN_PERHAPS_BROKEN_AWAY
                   | CLOSE_PERHAPS_BROKEN_AWAY
OPEN_PERHAPS_BROKEN_AWAY: "("
CLOSE_PERHAPS_BROKEN_AWAY: ")"

_WORD_SEPARATOR: " "


?any_word: lone_determinative
         | word

DIVIDER: DIVIDER_SYMBOL MODIFIER* FLAG
DIVIDER_SYMBOL: "|" | ":'" | ":\"" | ":." | "::" | ":" | ";" | "/"

OMISSION: OPEN_OMISSION
        | CLOSE_OMISSION
OPEN_OMISSION: "<<" | "<(" | "<"
CLOSE_OMISSION: ">>" | ")>" | ">"

BROKEN_AWAY: OPEN_BROKEN_AWAY
           | CLOSE_BROKEN_AWAY
OPEN_BROKEN_AWAY: "["
CLOSE_BROKEN_AWAY: "]"

INLINE_BROKEN_AWAY: OPEN_INLINE_BROKEN_AWAY 
                  | CLOSE_INLINE_BROKEN_AWAY
OPEN_INLINE_BROKEN_AWAY: /(\[\(|\[|(?<!{)\()/
CLOSE_INLINE_BROKEN_AWAY: /(\)\]|\)(?!})|\])/

lone_determinative.4: LD_PREFIX VARIANT (JOINER* VARIANT)* LD_SUFFIX
LD_PREFIX: OMISSION? OPEN_DETERMINATIVE OPEN_BROKEN_AWAY?
LD_SUFFIX: CLOSE_BROKEN_AWAY? CLOSE_DETERMINATIVE OMISSION?

word.3:  PART_JOINER? (INLINE_ERASURE | PARTS) (PART_JOINER? (INLINE_ERASURE | PARTS))* PART_JOINER?

INLINE_ERASURE: "°" PARTS? "\\" PARTS? "°"

PARTS: INNER_PARTS+
INNER_PARTS: (VARIANT | DETERMINATIVE) (PART_JOINER? (DETERMINATIVE | VARIANT | UNKNOWN_NUMBER_OF_SIGNS))*
           | UNKNOWN_NUMBER_OF_SIGNS (PART_JOINER? (DETERMINATIVE | VARIANT | UNKNOWN_NUMBER_OF_SIGNS))+

DETERMINATIVE: D_PREFIX VARIANT (PART_JOINER VARIANT)* D_SUFFIX
D_PREFIX: OMISSION? OPEN_DETERMINATIVE OPEN_BROKEN_AWAY?
D_SUFFIX: CLOSE_BROKEN_AWAY? CLOSE_DETERMINATIVE OMISSION?
OPEN_DETERMINATIVE: "{+" | "{"
CLOSE_DETERMINATIVE: "}"

PART_JOINER: ANY_CLOSE? JOINER ANY_OPEN?
           | ANY_CLOSE JOINER? ANY_OPEN?
           | ANY_CLOSE? JOINER? ANY_OPEN
ANY_CLOSE: (CLOSE_INLINE_BROKEN_AWAY  | CLOSE_OMISSION | CLOSE_LINGUISTIC_GLOSS)+
ANY_OPEN: (OPEN_LINGUISTIC_GLOSS | OPEN_OMISSION | OPEN_INLINE_BROKEN_AWAY)+
JOINER: "-" | "+" | "."
LINGUISTIC_GLOSS: OPEN_LINGUISTIC_GLOSS
                | CLOSE_LINGUISTIC_GLOSS
OPEN_LINGUISTIC_GLOSS: "{{"
CLOSE_LINGUISTIC_GLOSS: "}}"

VARIANT_OR_UNKNOWN: VARIANT | UNKNOWN_NUMBER_OF_SIGNS
VARIANT: VARIANT_PART (VARIANT_SEPARATOR VARIANT_PART)*
VARIANT_PART: UNKNOWN
            | VALUE_WITH_SIGN
            | VALUE 
            | COMPOUND_GRAPHEME 
            | GRAPHEME
            | DIVIDER
 
VALUE_WITH_SIGN: VALUE "!"? "(" ANY_GRAPHEME ")"

VALUE: VALUE_NAME SUB_INDEX? MODIFIER* FLAG
VALUE_NAME: VALUE_CHARACTER (INLINE_BROKEN_AWAY? VALUE_CHARACTER)*
          | LOGOGRAM_CHARACTER (INLINE_BROKEN_AWAY? LOGOGRAM_CHARACTER)?
VALUE_CHARACTER: "a" | "ā" | "â" | "b" | "d" | "e" | "ē" | "ê" | "g" | "h" 
               | "i" | "ī" | "î" | "y" | "k" | "l" | "m" | "n" | "p" | "q"
               | "r" | "s" | "ṣ" | "š" | "t" | "ṭ" | "u" | "ū" | "û" | "w"
               | "z" | "ḫ" | "ʾ" | "0".."9"
LOGOGRAM_CHARACTER : "A" | "Ā" | "Â" | "B" | "D" | "E" | "Ē" | "Ê" | "G" | "H" | "I"
                   | "Ī" | "Î" | "Y" | "K" | "L" | "M" | "N" | "P" | "Q" | "R" | "S"
                   | "Ṣ" | "Š" | "T" | "Ṭ" | "U" | "Ū" | "Û" | "W" | "Z" | "Ḫ" | "ʾ"
                   | "0".."9"
SUB_INDEX: ("₀".."₉" | "ₓ")+

ANY_GRAPHEME: COMPOUND_GRAPHEME | GRAPHEME
COMPOUND_GRAPHEME: "|" SUB_COMPOUND (COMPOUND_OPERATOR SUB_COMPOUND)* "|"
SUB_COMPOUND: "(" COMPOUND_PART (COMPOUND_OPERATOR COMPOUND_PART)* ")"
            | COMPOUND_PART
COMPOUND_PART: GRAPHEME (VARIANT_SEPARATOR GRAPHEME)*
COMPOUND_OPERATOR: "." | "×" | "%" | "&" | "+"

GRAPHEME: "$"?  GRAPHEME_NAME MODIFIER* FLAG
GRAPHEME_NAME: GRAPHEME_CHARACTER (INLINE_BROKEN_AWAY? GRAPHEME_CHARACTER)*
GRAPHEME_CHARACTER: "a".."z"
                  | "A".."Z"
                  | "Ṣ" | "Š" | "Ṭ"
                  | "ṣ" | "š" | "ṭ"
                  | "0".."9"
                  | "₀".."₉"

UNKNOWN: ("X" | "x") FLAG

VARIANT_SEPARATOR: "/"

FLAG: (UNCERTAIN | CORRECTION | COLLATION| DAMAGE)*
UNCERTAIN: "?"
CORRECTION: "!"
COLLATION: "*"
DAMAGE: "#"

MODIFIER: ("@" (MODIFIER_CHARACTER | ("0".."9")+))
MODIFIER_CHARACTER: "c" | "f" | "g" | "s" | "t" | "n" | "z" | "k" | "r" | "h" | "v"

UNKNOWN_NUMBER_OF_SIGNS: "..."
