?start: line

?line: empty_line
     | control_line
     | text_line

empty_line: /\s+/?

!control_line: ("=:" | "$" | "@" | "&" | "#") /.+/?

text_line: LINE_NUMBER _WORD_SEPARATOR+ text _WORD_SEPARATOR*
LINE_NUMBER: /[^ ]+/ "."


text: _head _tail* line_continuation?
_head: _have_both
     | _require_both
     | _omit_left
     | _omit_right+ _have_both?
_tail: _WORD_SEPARATOR _omit_right* _have_both?
     | _WORD_SEPARATOR? _omit_left+
     | _WORD_SEPARATOR _require_both

line_continuation: _WORD_SEPARATOR* LINE_CONTINUATION
LINE_CONTINUATION: "→"

_require_both: commentary_protocol
             | divider
             | divider_variant
_have_both: tabulation
         | column
         | language_shift
         | erasure
         | word
         | lone_determinative
         | unknown_number_of_signs
_omit_left.-1: cba
          | cpba
          | co
          | cdog
_omit_right.-1: oba
           | opba
           | oo
           | odog

divider.5: DIVIDER_SYMBOL modifiers flags
cba: CLOSE_BROKEN_AWAY
cpba: CLOSE_PERHAPS_BROKEN_AWAY
co: _close_omission
cdog: CLOSE_DOCUMENT_ORIENTED_GLOSS
oba: OPEN_BROKEN_AWAY
opba: OPEN_PERHAPS_BROKEN_AWAY
oo: _open_omission
odog: OPEN_DOCUMENT_ORIENTED_GLOSS
language_shift: SHIFT
unknown_number_of_signs: UNKNOWN_NUMBER_OF_SIGNS
omission_or_removal.-1: _omission
broken_away.-1: BROKEN_AWAY
perhaps_broken_away.-2: PERHAPS_BROKEN_AWAY

flags: (UNCERTAIN | CORRECTION | COLLATION| DAMAGE)*
modifiers: MODIFIER*

_NEW_LINE: /\n/

divider_variant: (variant_part | divider) "/" (variant_part | divider)
variant_part: VARIANT_PART

tabulation: TABULATION
TABULATION: "($___$)"

column: "&" number
number: ("0".."9")*

commentary_protocol: PROTOCOL_QUOTATION
                   | PROTOCOL_BASE_TEXT
                   | PROTOCOL_COMMENTARY
                   | PROTOCOL_UNCERTAIN
PROTOCOL_QUOTATION: "!qt"
PROTOCOL_BASE_TEXT: "!bs"
PROTOCOL_COMMENTARY: "!cm"
PROTOCOL_UNCERTAIN: "!zz"

DOCUMENT_ORIENTED_GLOSS: OPEN_DOCUMENT_ORIENTED_GLOSS
                       | CLOSE_DOCUMENT_ORIENTED_GLOSS
OPEN_DOCUMENT_ORIENTED_GLOSS: "{("
CLOSE_DOCUMENT_ORIENTED_GLOSS: ")}"

SHIFT: "%" ("a".."z" | "A".."Z" | "0".."9")+

erasure.-1: "°" erasure_part "\\" erasure_part "°"
erasure_part: ((DIVIDER | word | lone_determinative) (_WORD_SEPARATOR (DIVIDER | word | lone_determinative))*)?

PERHAPS_BROKEN_AWAY: OPEN_PERHAPS_BROKEN_AWAY
                   | CLOSE_PERHAPS_BROKEN_AWAY
OPEN_PERHAPS_BROKEN_AWAY: "("
CLOSE_PERHAPS_BROKEN_AWAY: ")"

_WORD_SEPARATOR: " "


?any_word: lone_determinative
         | word

DIVIDER: DIVIDER_SYMBOL MODIFIER* FLAG
DIVIDER_SYMBOL: "|" | ":'" | ":\"" | ":." | "::" | ":" | ";" | "/"

_omission: _open_omission | _close_omission
_open_omission: OPEN_OMISSION
_close_omission: CLOSE_OMISSION
OMISSION: OPEN_OMISSION
        | CLOSE_OMISSION
OPEN_OMISSION: "<<" | "<(" | "<"
CLOSE_OMISSION:  ">>" | ")>" | ">"

BROKEN_AWAY: OPEN_BROKEN_AWAY
           | CLOSE_BROKEN_AWAY
OPEN_BROKEN_AWAY: "["
CLOSE_BROKEN_AWAY: "]"

_inline_broken_away: _open_inline_broken_away
                   | _close_inline_broken_away
_open_inline_broken_away: OPEN_BROKEN_AWAY | OPEN_PERHAPS_BROKEN_AWAY
_close_inline_broken_away: CLOSE_BROKEN_AWAY | CLOSE_PERHAPS_BROKEN_AWAY
INLINE_BROKEN_AWAY: OPEN_INLINE_BROKEN_AWAY
                  | CLOSE_INLINE_BROKEN_AWAY
OPEN_INLINE_BROKEN_AWAY: /(\[\(|\[|(?<!{)\()/
CLOSE_INLINE_BROKEN_AWAY: /(\)\]|\)(?!})|\])/

lone_determinative.4: _d_prefix VARIANT (_joiner* VARIANT)* _d_suffix

word.3:  _joiner? _any_open? (_inline_erasure | _parts) (_part_joiner? (_inline_erasure | _parts))* _any_close? _joiner?

!_inline_erasure: "°" _parts? "\\" _parts? "°"

_parts: (VARIANT | (_determinative+ (VARIANT | UNKNOWN_NUMBER_OF_SIGNS)?)) _parts_tail*
      | UNKNOWN_NUMBER_OF_SIGNS _parts_tail+
_parts_tail: _part_joiner (VARIANT | UNKNOWN_NUMBER_OF_SIGNS)
           | _part_joiner? _determinative+ (VARIANT | UNKNOWN_NUMBER_OF_SIGNS)?

_determinative: _d_prefix VARIANT (_part_joiner VARIANT)* _d_suffix
_d_prefix: _omission? OPEN_DETERMINATIVE OPEN_BROKEN_AWAY?
_d_suffix: CLOSE_BROKEN_AWAY? CLOSE_DETERMINATIVE _omission?
OPEN_DETERMINATIVE: "{+" | "{"
CLOSE_DETERMINATIVE: "}"

_part_joiner: IN_WORD_NEWLINE? _any_close? _joiner _any_open?
            | IN_WORD_NEWLINE? _any_close _joiner? _any_open?
            | IN_WORD_NEWLINE? _any_close? _joiner? _any_open
_any_close: (_close_inline_broken_away | _close_omission | _close_linguistic_gloss)+
_any_open: (_open_linguistic_gloss | _open_omission | _open_inline_broken_away)+
!_joiner: "-" | "+" | "." | ":"

!_open_linguistic_gloss: "{{"
!_close_linguistic_gloss: "}}"

VARIANT_OR_UNKNOWN: VARIANT | UNKNOWN_NUMBER_OF_SIGNS
VARIANT: VARIANT_PART (VARIANT_SEPARATOR VARIANT_PART)*
VARIANT_PART: UNKNOWN
            | VALUE_WITH_SIGN
            | VALUE
            | COMPOUND_GRAPHEME
 
VALUE_WITH_SIGN: VALUE "!"? "(" ANY_GRAPHEME ")"

VALUE: VALUE_NAME SUB_INDEX? MODIFIER* FLAG
VALUE_NAME: VALUE_CHARACTER (INLINE_BROKEN_AWAY? VALUE_CHARACTER)*
          | LOGOGRAM_CHARACTER (INLINE_BROKEN_AWAY? LOGOGRAM_CHARACTER)*
VALUE_CHARACTER: "a" | "ā" | "â" | "b" | "d" | "e" | "ē" | "ê" | "g" | "h" 
               | "i" | "ī" | "î" | "y" | "k" | "l" | "m" | "n" | "p" | "q"
               | "r" | "s" | "ṣ" | "š" | "t" | "ṭ" | "u" | "ū" | "û" | "w"
               | "z" | "ḫ" | "ʾ" | "0".."9"
LOGOGRAM_CHARACTER : "A" | "Ā" | "Â" | "B" | "D" | "E" | "Ē" | "Ê" | "G" | "H" | "I"
                   | "Ī" | "Î" | "Y" | "K" | "L" | "M" | "N" | "P" | "Q" | "R" | "S"
                   | "Ṣ" | "Š" | "T" | "Ṭ" | "U" | "Ū" | "Û" | "W" | "Z" | "Ḫ" | "ʾ"
                   | "0".."9"
SUB_INDEX: ("₀".."₉" | "ₓ")+

ANY_GRAPHEME: COMPOUND_GRAPHEME | GRAPHEME
COMPOUND_GRAPHEME: "|" SUB_COMPOUND (COMPOUND_OPERATOR SUB_COMPOUND)* "|"
SUB_COMPOUND: "(" COMPOUND_PART (COMPOUND_OPERATOR COMPOUND_PART)* ")"
            | COMPOUND_PART
COMPOUND_PART: GRAPHEME (VARIANT_SEPARATOR GRAPHEME)*
COMPOUND_OPERATOR: "." | "×" | "%" | "&" | "+"

GRAPHEME: GRAPHEME_NAME MODIFIER* FLAG
GRAPHEME_NAME: GRAPHEME_CHARACTER (INLINE_BROKEN_AWAY? GRAPHEME_CHARACTER)*
GRAPHEME_CHARACTER: "a".."z"
                  | "A".."Z"
                  | "Ṣ" | "Š" | "Ṭ"
                  | "ṣ" | "š" | "ṭ"
                  | "0".."9"
                  | "₀".."₉"

UNKNOWN: ("X" | "x") FLAG

VARIANT_SEPARATOR: "/"

FLAG: (UNCERTAIN | CORRECTION | COLLATION| DAMAGE)*
UNCERTAIN: "?"
CORRECTION: "!"
COLLATION: "*"
DAMAGE: "#"

MODIFIER: ("@" (MODIFIER_CHARACTER | ("0".."9")+))
MODIFIER_CHARACTER: "c" | "f" | "g" | "s" | "t" | "n" | "z" | "k" | "r" | "h" | "v"

IN_WORD_NEWLINE: ";"

UNKNOWN_NUMBER_OF_SIGNS: "..."
