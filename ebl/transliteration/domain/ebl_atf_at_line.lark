%import .ebl_atf_common (INT, LCASE_LETTER)
%import .ebl_atf_common (free_text)
%import .ebl_atf_common (object, OBJECT, generic_object, fragment)
%import .ebl_atf_common (surface, SURFACE, generic_surface, face, edge)
%import .ebl_atf_common (seal, OBJECT, SURFACE, free_text)

?at_line : "@" value

?value :  seal | column | heading | discourse | object_with_status | surface_with_status | divisions | composite

STATUS:  "'" | "?" | "!" | "*"

_WHITESPACE: " "

statuses: STATUS*

surface_with_status : surface _WHITESPACE? statuses

object_with_status : object _WHITESPACE? statuses

column : "column " INT _WHITESPACE? statuses

heading: "h"INT

!discourse: "catchline" | "colophon" | "date" | "signature" | "signatures" | "summary" | "witnesses"

divisions: "m=division " free_text [_WHITESPACE INT]

?composite: composite_start | composite_end
composite_start: "div " free_text [_WHITESPACE INT]
composite_end: "end " free_text
