%import common.NEWLINE
%import common.WS_INLINE
%import .ebl_atf_text_line (text_line, any_word, note_line, parallel_line)
%import .ebl_atf_dollar_line (dollar_line)
%import .ebl_atf_at_line (at_line)
%import .ebl_atf_labels (labels)

?start: line

?line: empty_line
     | note_line
     | text_line
     | dollar_line
     | at_line
     | control_line
     | parallel_line

empty_line: /\s+/?
!control_line.-2: ("=:" | "&" | "#") /.+/?


?paratext: note_line | dollar_line

?chapter: chapter_line

chapter_line: line_variant (_NEWLINE line_variant)*

line_variant: reconstruction (_NEWLINE manuscript_line)*

reconstruction: text_line [_NEWLINE note_line] (_NEWLINE parallel_line)*

manuscript_line: _WHITE_SPACE? siglum  [" " labels] manuscript_text paratext_line*
?manuscript_text: " " text_line | empty_line
?paratext_line: _NEWLINE _WHITE_SPACE? paratext
_WHITE_SPACE: WS_INLINE+
_NEWLINE: NEWLINE

siglum: [PROVENACE] PERIOD [TYPE] [DISAMBIQUATOR]
PROVENACE: "Assa"
         | "Ašš"
         | "Huz"
         | "Kal"
         | "Kho"
         | "Nin"
         | "Tar"
         | "Baba"
         | "Bab"
         | "Bor"
         | "Cut"
         | "Dil"
         | "Isn"
         | "Kiš"
         | "Lar"
         | "Met"
         | "Nēr"
         | "Nip"
         | "Sip"
         | "Šad"
         | "Ur"
         | "Urk"
         | "Ala"
         | "Ama"
         | "Emr"
         | "Hat"
         | "Mar"
         | "Meg"
         | "Sus"
         | "Uga"
         | "Unc"
PERIOD: "Ur3"
      | "OA"
      | "OB"
      | "MB"
      | "MA"
      | "Hit"
      | "NA"
      | "NB"
      | "LB"
      | "Per"
      | "Hel"
      | "Par"
      | "Unc"
TYPE: "Sch"
    | "Com"
    | "Quo"
    | "Var"
    | "Ex"
    | "Par"
DISAMBIQUATOR: /[\S]+/
